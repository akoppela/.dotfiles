#+TITLE: Emacs Configuration
#+AUTHOR: Andrey Koppel (akoppela)
#+EMAIL: akoppela@gmail.com

* Personal info.

  #+BEGIN_SRC emacs-lisp
    (setq user-full-name "Andrey Koppel")
    (setq user-mail-address "akoppela@gmail.com")
  #+END_SRC

* Package management.

** Initialize =package.el= and include Org, Gnu and Melpa package archives.

   #+BEGIN_SRC emacs-lisp
     (require 'package)
     (setq package-enable-at-startup nil)
     (setq package-archives '(("org" . "https://orgmode.org/elpa/")
                              ("gnu" . "https://elpa.gnu.org/packages/")
                              ("melpa" . "https://melpa.org/packages/")))
     (package-initialize)
   #+END_SRC

** Make sure =use-package= is installed.

   #+BEGIN_SRC emacs-lisp
     (unless (package-installed-p 'use-package)
       (package-refresh-contents)
       (package-install 'use-package))
   #+END_SRC

** Initialize =use-package= and set it up to install the package if it's not available yet.

   #+BEGIN_SRC emacs-lisp
     (require 'use-package)
     (setq use-package-always-ensure t)
   #+END_SRC

** Always compile packages, and use the newest version available.

   #+BEGIN_SRC emacs-lisp
     (use-package auto-compile
       :config
       (auto-compile-on-load-mode 1))

     (setq load-prefer-newer t)
   #+END_SRC

* Defaults.

** Setup encoding to UTF-8.

   #+BEGIN_SRC emacs-lisp
     (setq coding-system-for-read 'utf-8)
     (setq coding-system-for-write 'utf-8)
   #+END_SRC

** Start with users home directory by default.

   #+BEGIN_SRC emacs-lisp
     (setq default-directory "~/")
   #+END_SRC

** Increase garbage collection threshold.

   This means GC runs less often, which speeds up some operations.

   #+BEGIN_SRC emacs-lisp
     (use-package gcmh
       :config
       (gcmh-mode 1))
   #+END_SRC

** Delete trailing whitespace everytime buffer is saved.

   #+BEGIN_SRC emacs-lisp
     (add-hook 'before-save-hook 'delete-trailing-whitespace)
   #+END_SRC

** Always follow symlinks when opening a file.

   #+BEGIN_SRC emacs-lisp
     (setq vc-follow-symlinks t)
   #+END_SRC

** Make sure sentenses does not have two spaces after periods.

   #+BEGIN_SRC emacs-lisp
     (setq sentense-end-double-space nil)
   #+END_SRC

** Ask before closing Emacs.

   #+BEGIN_SRC emacs-lisp
     (setq confirm-kill-emacs 'y-or-n-p)
   #+END_SRC

** Set backup files directory.

   #+BEGIN_SRC emacs-lisp
     (setq backup-directory-alist '(("." . "~/.emacs.d/backups")))
   #+END_SRC

** Set auto save files directory.

   #+BEGIN_SRC emacs-lisp
     (setq auto-save-file-name-transforms '((".*" "~/.emacs.d/auto-save-list/" t)))
   #+END_SRC

** Shorten yes/no answers.

   #+BEGIN_SRC emacs-lisp
     (defalias 'yes-or-no-p 'y-or-n-p)
   #+END_SRC

** Open large files with =vlf=.

   #+BEGIN_SRC emacs-lisp
     (use-package vlf
       :commands vlf
       :config
       (require 'vlf-setup))
   #+END_SRC

** Do not add newline at the end of a file.

   #+BEGIN_SRC emacs-lisp
     (setq require-final-newline nil)
     (setq mode-require-final-newline nil)
   #+END_SRC

** Set =eww= as default browser.

   #+BEGIN_SRC emacs-lisp
     (setq browse-url-browser-function 'eww-browse-url)
   #+END_SRC

** Make session persistent.

   #+BEGIN_SRC emacs-lisp
     (setq desktop-path (list user-emacs-directory))
     (setq desktop-globals-to-save
           '((comint-input-ring . 50)
             (compile-history . 30)
             desktop-missing-file-warning
             (dired-regexp-history . 20)
             (extended-command-history . 30)
             (face-name-history . 20)
             (file-name-history . 100)
             (grep-find-history . 30)
             (grep-history . 30)
             (ivy-history . 100)
             (magit-revision-history . 50)
             (minibuffer-history . 50)
             (org-clock-history . 50)
             (org-refile-history . 50)
             (org-tags-history . 50)
             (query-replace-history . 60)
             (read-expression-history . 60)
             (regexp-history . 60)
             (regexp-search-ring . 20)
             register-alist
             (search-ring . 20)
             (shell-command-history . 50)
             tags-file-name
             tags-table-list))
     (desktop-save-mode 1)

     (setq-default history-length 1000)
     (add-hook 'after-init-hook 'savehist-mode)

     (use-package session
       :config
       (setq session-save-file (expand-file-name ".session" user-emacs-directory))
       (setq session-save-file-coding-system 'utf-8)
       (add-hook 'after-init-hook 'session-initialize))
   #+END_SRC

** Enable smart parenthesis.

   #+BEGIN_SRC emacs-lisp
     (use-package smartparens
       :commands smartparens-mode
       :init
       (add-hook 'prog-mode-hook 'smartparens-mode)
       :config
       (require 'smartparens-config))
   #+END_SRC

** Revert TAGS file without asking.

   #+BEGIN_SRC emacs-lisp
     (setq tags-revert-without-query t)
   #+END_SRC

** Load =dash=.

   #+BEGIN_SRC emacs-lisp
     (use-package dash)
   #+END_SRC

** Truncate eshell buffer.

   #+BEGIN_SRC emacs-lisp
     (add-hook 'eshell-output-filter-functions #'eshell-truncate-buffer)
   #+END_SRC

** Automatically rescan =imenu=.

   #+BEGIN_SRC emacs-lisp
     (setq imenu-auto-rescan t)
   #+END_SRC

** Startup profiler.

   #+BEGIN_SRC emacs-lisp
     (use-package esup
       :commands esup)
   #+END_SRC

** Do not lock files.

   #+BEGIN_SRC emacs-lisp
     (setq create-lockfiles nil)
   #+END_SRC

** Proper terminal.

   #+BEGIN_SRC emacs-lisp
     ;; (use-package vterm)
   #+END_SRC

** =direnv= support.

   #+BEGIN_SRC emacs-lisp
     (use-package envrc
       :commands envrc-mode
       :init
       (add-hook 'prog-mode-hook 'envrc-mode))
   #+END_SRC

* Keybindings.

** =evil= provides =vi= keybindings.

*** Initialize =evil=.

    #+BEGIN_SRC emacs-lisp
      (use-package evil
        :init
        (setq evil-want-C-i-jump nil)
        (setq evil-want-integration t)
        (setq evil-want-keybinding nil)
        (setq evil-undo-system 'undo-tree) ;; TODO: Change to native undo-redo from Emacs 28
        :config
        (evil-mode 1))
    #+END_SRC

*** Enable =evil-collection=.

    #+BEGIN_SRC emacs-lisp
      (use-package evil-collection
        :after evil
        :init
        (setq evil-collection-outline-bind-tab-p nil)
        (setq evil-collection-company-use-tng nil)
        :config
        (evil-collection-init))
    #+END_SRC

*** Enable =evil-surround=.

    #+BEGIN_SRC emacs-lisp
      (use-package evil-surround
        :after evil-collection
        :config
        (global-evil-surround-mode 1))
    #+END_SRC

*** Enable =evil-commentary=.

    #+BEGIN_SRC emacs-lisp
      (use-package evil-commentary
        :after evil-collection
        :config
        (evil-commentary-mode 1))
    #+END_SRC

*** Define default normal states.

    #+BEGIN_SRC emacs-lisp
      (add-to-list 'evil-normal-state-modes 'rcirc-mode)
    #+END_SRC

** =hydra=.

   #+BEGIN_SRC emacs-lisp
     (use-package hydra)
   #+END_SRC

** =general= makes it easier to assign keybindings.

*** Initialize.

    #+BEGIN_SRC emacs-lisp
      (use-package general
        :config
        (general-create-definer leader-def
          :states '(normal visual insert motion emacs)
          :keymaps 'override
          :prefix "SPC"
          :non-normal-prefix "<C-M-return>")
        (general-create-definer major-def
          :states '(normal visual insert motion emacs)
          :keymaps 'override
          :prefix "<C-M-escape>"
          :non-normal-prefix "<C-M-escape>"))
    #+END_SRC

*** Main menu.

**** Helper functions.

     #+BEGIN_SRC emacs-lisp
       (defun my/counsel-projectile-rg ()
         "Calls counsel-projectile-rg with no initial input"
         (interactive)
         (progn
           (setq counsel-projectile-rg-initial-input nil)
           (counsel-projectile-rg)))

       (defun my/counsel-projectile-rg-at-point ()
         "Calls counsel-projectile-rg with ivy-at-point"
         (interactive)
         (progn
           (setq counsel-projectile-rg-initial-input (ivy-thing-at-point))
           (counsel-projectile-rg)))
     #+END_SRC

**** Keybindings.

     #+BEGIN_SRC emacs-lisp
       (leader-def
         "" nil
         "SPC" '(counsel-M-x :which-key "M-x")
         "u" '(universal-argument :which-key "universal argument")
         "/" '(my/counsel-projectile-rg :which-key "find in project")
         "*" '(my/counsel-projectile-rg-at-point :which-key "find in project at point"))
     #+END_SRC
*** Buffer.

    #+BEGIN_SRC emacs-lisp
      (leader-def
        "b" '(:ignore t :which-key "buffer")
        "b b" '(ivy-switch-buffer :which-key "switch")
        "b l" '(ibuffer :which-key "list")
        "b d" '(kill-current-buffer :which-key "delete")
        "b x" '(kill-buffer-and-window :which-key "delete with window")
        "b s" '(save-some-buffers :which-key "save")
        "b e" '(eval-buffer :which-key "eval")
        "b r" '(rename-buffer :which-key "rename")
        "b R" '(revert-buffer :which-key "revert"))

      (general-def
        :states '(normal visual)
        :keymaps 'ibuffer-mode-map
        "q" 'kill-buffer-and-window)
    #+END_SRC

*** Window.

**** Helper functions.

***** Resize hydra.

      #+BEGIN_SRC emacs-lisp
        (defhydra hydra-window-resize ()
          "Resize window"
          ("[" shrink-window-horizontally "shrink horizontally")
          ("]" enlarge-window-horizontally "enlarge horizontally")
          ("{" shrink-window "shrink vertically")
          ("}" enlarge-window "enlarge vertically"))
      #+END_SRC

***** Toggle split from horizontal to vertical and vice versa.

      #+BEGIN_SRC emacs-lisp
        (defun my/split-window-toggle ()
          "Toggles window split from horizontal to vertical and vice versa."
          (interactive)
          (if (= (count-windows) 2)
              (let* ((this-win-buffer (window-buffer))
                     (next-win-buffer (window-buffer (next-window)))
                     (this-win-edges (window-edges (selected-window)))
                     (next-win-edges (window-edges (next-window)))
                     (this-win-2nd (not (and (<= (car this-win-edges)
                                                 (car next-win-edges))
                                             (<= (cadr this-win-edges)
                                                 (cadr next-win-edges)))))
                     (splitter
                      (if (= (car this-win-edges)
                             (car (window-edges (next-window))))
                          'split-window-horizontally
                        'split-window-vertically)))
                (delete-other-windows)
                (let ((first-win (selected-window)))
                  (funcall splitter)
                  (if this-win-2nd (other-window 1))
                  (set-window-buffer (selected-window) this-win-buffer)
                  (set-window-buffer (next-window) next-win-buffer)
                  (select-window first-win)
                  (if this-win-2nd (other-window 1))))))
      #+END_SRC

**** Keybindings.

     #+BEGIN_SRC emacs-lisp
       (leader-def
         "w" '(:ignore t :which-key "window")
         "w TAB" '(other-window :which-key "next")
         "w d" '(delete-window :which-key "delete")
         "w D" '(delete-other-windows :which-key "delete other")
         "w r" '(hydra-window-resize/body :which-key "resize")
         "w a" '(ace-window :which-key "ace")

         "w s" '(:ignore t :which-key "split")
         "w s h" '(split-window-below :which-key "horizontally")
         "w s v" '(split-window-right :which-key "vertically")
         "w s t" '(my/split-window-toggle :which-key "toggle"))
     #+END_SRC

*** Theme.

**** Helper functions.

***** Change theme.

      #+BEGIN_SRC emacs-lisp
        (defvar my/change-theme-hook nil
          "Hooks to run after theme is changed.")

        (defmacro my/change-theme (fun-name fun-description themes get-new-theme get-rest-themes sort-themes)
          "Changes theme based on given data"
          `(defun ,fun-name ()
             ,fun-description
             (interactive)
             (let* ((new-theme (,get-new-theme ,themes))
                    (rest-themes (,get-rest-themes ,themes))
                    (new-available-themes (funcall (,sort-themes 'append) rest-themes (list new-theme))))
               (progn
                 (setq ,themes new-available-themes)
                 (if (eq new-theme my/current-theme)
                     (,fun-name)
                   (progn
                     (setq my/current-theme new-theme)
                     (mapcar #'disable-theme custom-enabled-themes)
                     (font-lock-mode)
                     (load-theme new-theme t)
                     (run-hooks 'my/change-theme-hook)
                     (font-lock-mode)))))))
      #+END_SRC

***** Next theme.

      #+BEGIN_SRC emacs-lisp
        (my/change-theme my/next-theme "Changes theme to next one" my/themes car cdr identity)
      #+END_SRC

***** Previous theme.

      #+BEGIN_SRC emacs-lisp
        (my/change-theme my/previous-theme "Changes theme to previous one" my/themes -last-item butlast -flip)
      #+END_SRC

***** Theme hydra.

      #+BEGIN_SRC emacs-lisp
        (defhydra hydra-change-theme ()
          "Change theme"
          ("n" my/next-theme "next")
          ("N" my/previous-theme "previous"))
      #+END_SRC

**** Keybindings.

     #+BEGIN_SRC emacs-lisp
       (leader-def
         "t" '(hydra-change-theme/body :which-key "theme"))
     #+END_SRC

*** File.

**** Helper functions.

     #+BEGIN_SRC emacs-lisp
       (defun my/delete-file-and-buffer ()
         "Kill the current buffer and delete the file it's visiting."
         (interactive)
         (let ((filename (buffer-file-name)))
           (if filename
               (if (vc-backend filename)
                   (vc-delete-file filename)
                 (progn (delete-file filename)
                        (message "Deleted file %s." filename)
                        (kill-buffer)))
             (message "Can't delete file."))))
     #+END_SRC

**** Bindings.

     #+BEGIN_SRC emacs-lisp
       (leader-def
         "f" '(:ignore t :which-key "file")
         "f f" '(counsel-find-file :which-key "find")
         "f s" '(save-buffer :which-key "save")
         "f r" '(rename-file :which-key "rename")
         "f d" '(my/delete-file-and-buffer :which-key "delete")
         "f c" '(copy-file :which-key "copy")

         "f e" '(:ignore t :which-key "emacs")
         "f e c" '(my/open-configuration :which-key "configuration")
         "f e r" '(my/load-configuration :which-key "reload configuration"))
     #+END_SRC

*** Project.

    #+BEGIN_SRC emacs-lisp
      (leader-def
        "p" '(:ignore t :which-key "project")
        "p f" '(counsel-projectile-find-file :which-key "find file")
        "p p" '(counsel-projectile-switch-project :which-key "switch")
        "p b" '(counsel-projectile-switch-to-buffer :which-key "buffer")
        "p t" '(treemacs :which-key "treemacs"))
    #+END_SRC

*** Application.

**** Helper functions.

     #+BEGIN_SRC emacs-lisp
       (defun my/eshell ()
         "Starts eshell using projectile if possible."
         (interactive)
         (if (projectile-project-p)
             (projectile-run-eshell nil)
           (eshell)))
     #+END_SRC

**** Keybindings.

     #+BEGIN_SRC emacs-lisp
       (leader-def
         "a" '(:ignore t :which-key "application")
         "a i" '(rcirc :which-key "IRC")
         "a e" '(my/eshell :which-key "eshell"))
     #+END_SRC

*** Search.

    #+BEGIN_SRC emacs-lisp
      (leader-def
        "s" '(:ignore t :which-key "search")
        "s s" '(swiper-isearch :which-key "buffer")
        "s S" '(swiper-isearch-thing-at-point :which-key "buffer with thing at point")
        "s b" '(eww-search-words :which-key "browser")
        "s i" '(imenu :which-key "imenu"))
    #+END_SRC

*** Error.

    #+BEGIN_SRC emacs-lisp
      (leader-def
        "e" '(:ignore t :which-key "error")
        "e v" '(flycheck-verify-setup :which-key "verify setup")
        "e n" '(flycheck-next-error :which-key "next")
        "e N" '(flycheck-previous-error :which-key "previous")
        "e l" '(flycheck-list-errors :which-key "list")
        "e w" '(flyspell-auto-correct-word :which-key "auto correct word"))
    #+END_SRC

*** Git.

    #+BEGIN_SRC emacs-lisp
      (leader-def
        "g" '(:ignore t :which-key "git")
        "g s" '(magit-status :which-key "status")
        "g b" '(magit-blame-addition :which-key "blame")
        "g c" '(magit-clone :which-key "clone")
        "g h" '(magit-log-buffer-file :which-key "history"))
    #+END_SRC

*** Narrow.

    #+BEGIN_SRC emacs-lisp
      (leader-def
        "n" '(:ignore t :which-key "narrow")
        "n f" '(narrow-to-defun :which-key "function")
        "n r" '(narrow-to-region :which-key "region")
        "n p" '(narrow-to-page :which-key "page")
        "n w" '(widen :which-key "widen"))

      (leader-def
        :keymaps '(org-mode-map outline-minor-mode-map)
        "n s" '(org-narrow-to-subtree :which-key "subtree"))
    #+END_SRC

*** Jump.

    #+BEGIN_SRC emacs-lisp
      (leader-def
        "j" '(:ignore t :which-key "jump")
        "j s" '(avy-goto-subword-1 :which-key "subword")
        "j w" '(avy-goto-word-1 :which-key "word"))
    #+END_SRC

*** Help.

    #+BEGIN_SRC emacs-lisp
      (leader-def
        "h" '(:ignore t :which-key "help")
        "h a" '(counsel-apropos :which-key "apropos")
        "h p" '(helpful-at-point :which-key "at point")
        "h P" '(describe-package :which-key "package")
        "h f" '(counsel-describe-function :which-key "describe function")
        "h v" '(counsel-describe-variable :which-key "describe variable")
        "h s" '(helpful-symbol :which-key "describe symbol")
        "h k" '(helpful-key :which-key "describe key")
        "h m" '(describe-mode :which-key "describe mode")
        "h i" '(info :which-key "info")
        "h b" '(benchmark-init/show-durations-tabulated :which-key "benchmark emacs initialization"))
    #+END_SRC

*** Quit.

    #+BEGIN_SRC emacs-lisp
      (leader-def
        "q" '(:ignore t :which-key "quit")
        "q q" '(save-buffers-kill-terminal :which-key "client")
        "q Q" '(save-buffers-kill-emacs :which-key "server"))
    #+END_SRC

* Appearance.

** Hide default Emacs screen.

   #+BEGIN_SRC emacs-lisp
     (setq inhibit-startup-screen t)
   #+END_SRC

** Use =all-the-icons=.

   #+BEGIN_SRC emacs-lisp
     (use-package all-the-icons)
   #+END_SRC

** Enable custom theme.

   #+BEGIN_SRC emacs-lisp
     (use-package doom-themes
       :config
       ;; Global settings (defaults)
       (setq doom-themes-enable-bold nil)
       (setq doom-themes-enable-italic nil)

       ;; Load and enable theme
       (load-theme 'doom-moonlight t)

       ;; Enable flashing mode-line on errors
       (doom-themes-visual-bell-config)

       ;; Use the colorful treemacs theme
       (setq doom-themes-treemacs-theme "doom-colors")
       (doom-themes-treemacs-config)

       ;; Corrects (and improves) org-mode's native fontification.
       (doom-themes-org-config))
   #+END_SRC

** =mode-line= specific.

*** Enable custom modeline.

    #+BEGIN_SRC emacs-lisp
      (use-package doom-modeline
        :init (doom-modeline-mode 1))
    #+END_SRC

*** Show full names for evil state.

    #+BEGIN_SRC emacs-lisp
      (setq evil-normal-state-tag "N")
      (setq evil-insert-state-tag "I")
      (setq evil-visual-state-tag "V")
      (setq evil-replace-state-tag "R")
      (setq evil-operator-state-tag "O")
      (setq evil-motion-state-tag "M")
      (setq evil-emacs-state-tag "E")
    #+END_SRC

*** Show match info.

    #+BEGIN_SRC emacs-lisp
      (use-package anzu
        :config
        (setq anzu-cons-mode-line-p nil)
        (global-anzu-mode 1))

      (use-package evil-anzu
        :after (evil-collection anzu))
    #+END_SRC

*** Show date and time.

    #+BEGIN_SRC emacs-lisp
      (defun padDateNumber (stringNumber)
        (format "%02d" (string-to-number stringNumber)))

      (setq display-time-string-forms
            '(24-hours ":" minutes " " dayname " "  (padDateNumber day) "/" (padDateNumber month) "/" year))

      (display-time-mode 1)
    #+END_SRC

** Hide menu, tool and scroll bars.

   #+BEGIN_SRC emacs-lisp
     (tool-bar-mode 0)
     (scroll-bar-mode 0)
     (menu-bar-mode 0)
   #+END_SRC

** Enable current line highlighting.

   #+BEGIN_SRC emacs-lisp
     (global-hl-line-mode 1)
   #+END_SRC

** Turn on syntax highlighting whenever possible.

   #+BEGIN_SRC emacs-lisp
     (global-font-lock-mode 1)
   #+END_SRC

** Visually indicate matching parentheses.

   #+BEGIN_SRC emacs-lisp
     (show-paren-mode 1)
     (setq show-paren-delay 0.0)
   #+END_SRC

** Flash screen on invalid operation.

   #+BEGIN_SRC emacs-lisp
     (setq visible-bell t)
   #+END_SRC

** Display visual line numbers.

   Visual lines are relative screen lines.

   #+BEGIN_SRC emacs-lisp
     (global-display-line-numbers-mode)
     (setq display-line-numbers-type 'visual)
     (setq display-line-numbers-width-start t)
   #+END_SRC

** Always indent with spaces

   #+BEGIN_SRC emacs-lisp
     (setq-default indent-tabs-mode nil)
   #+END_SRC

** Use 4 spaces for tabs.

   #+BEGIN_SRC emacs-lisp
     (setq-default tab-width 4)
   #+END_SRC

** Smooth scroll.

   #+BEGIN_SRC emacs-lisp
     (setq scroll-conservatively 100)
   #+END_SRC

** Center cursor vertically.

   #+BEGIN_SRC emacs-lisp
     (use-package centered-cursor-mode
       :commands centered-cursor-mode
       :init
       (add-hook 'prog-mode-hook 'centered-cursor-mode))
   #+END_SRC

** Buffer list grouping.

   #+BEGIN_SRC emacs-lisp
     (use-package ibuffer-vc
       :commands ibuffer-vc-set-filter-groups-by-vc-root
       :init
       (add-hook 'ibuffer-hook
                 (lambda ()
                   (ibuffer-vc-set-filter-groups-by-vc-root)
                   (ibuffer-do-sort-by-recency)))
       :config
       (setq ibuffer-formats
             '((mark modified read-only locked vc-status-mini
                     " "
                     (name 18 18 :left :elide)
                     " "
                     (size 9 -1 :right)
                     " "
                     (mode 16 16 :left :elide)
                     " "
                     vc-relative-file))))
   #+END_SRC

** Add color background for hexadecimal strings.

   #+BEGIN_SRC emacs-lisp
     (use-package rainbow-mode
       :commands rainbow-mode
       :init
       (add-hook 'prog-mode-hook 'rainbow-mode))
   #+END_SRC

** Enable smart expand region.

   #+BEGIN_SRC emacs-lisp
     (use-package expand-region
       :after general
       :commands er/expand-region
       :init
       (leader-def
         "v" '(er/expand-region :which-key "expand region")))
   #+END_SRC

* Navigation, search and completion.

** =counsel= completion framework.

   #+BEGIN_SRC emacs-lisp
     (use-package counsel
       :init
       (setq ivy-re-builders-alist '((t . ivy--regex-ignore-order)))
       :config
       (ivy-mode 1)
       (major-def
         :keymaps 'ivy-minibuffer-map
         "o" '(ivy-occur :which-key "occur")
         "a" '(ivy-read-action :which-key "action")))
   #+END_SRC

** =wgrep= to edit search.

   #+BEGIN_SRC emacs-lisp
     (use-package wgrep
       :commands ivy-wgrep-change-to-wgrep-mode)
   #+END_SRC

** =treemacs= file explorer.

   #+BEGIN_SRC emacs-lisp
     (use-package treemacs
       :commands treemacs)

     (use-package treemacs-evil
       :after (treemacs evil-collection))

     (use-package treemacs-projectile
       :after (treemacs projectile))
   #+END_SRC

** =iedit= to edit multiple regions simultaneously.

   #+BEGIN_SRC emacs-lisp
     (use-package iedit
       :commands iedit-mode)
   #+END_SRC

** =company= enables auto-completion.

   #+BEGIN_SRC emacs-lisp
     (defun my/company-complete-common-or-cycle-backward ()
       "Complete common prefix or cycle backward."
       (interactive)
       (company-complete-common-or-cycle -1))

     (use-package company
       :commands company-mode
       :init
       (setq company-idle-delay 0)
       (setq company-require-match nil)
       (setq company-minimum-prefix-length 2)
       (setq company-dabbrev-downcase nil)
       (setq company-dabbrev-ignore-case nil)
       (add-hook 'prog-mode-hook 'company-mode)
       :config
       (general-def
         :keymaps 'company-active-map
         "TAB" 'company-complete-common-or-cycle
         "<backtab>" 'my/company-complete-common-or-cycle-backward))
   #+END_SRC

** =flycheck= checks syntax.

   #+BEGIN_SRC emacs-lisp
     (use-package flycheck
       :config
       (setq flycheck-check-syntax-automatically '(mode-enabled save)))
   #+END_SRC

** =flyspell= checks spelling.

   #+BEGIN_SRC emacs-lisp
     (use-package flyspell
       :commands flyspell-mode
       :init
       (add-hook 'text-mode-hook 'flyspell-mode)
       (add-hook 'prog-mode-hook 'flyspell-prog-mode)
       (add-hook 'org-mode-hook 'flyspell-mode)
       (add-hook 'git-commit-mode-hook 'flyspell-mode))
   #+END_SRC

** =undo-tree=.

   #+BEGIN_SRC emacs-lisp
     (use-package undo-tree
       :config
       (global-undo-tree-mode))
   #+END_SRC

* Project, time and task management.

** =projectile=.

   #+BEGIN_SRC emacs-lisp
     (use-package projectile
       :config
       (projectile-mode 1)
       (setq projectile-completion-system 'ivy))

     (use-package counsel-projectile
       :after (projectile counsel)
       :config
       (counsel-projectile-mode 1))
   #+END_SRC

** =magit= for Git related stuff.

*** Initialization.

    #+BEGIN_SRC emacs-lisp
      (use-package magit
        :commands magit
        :init
        (setq magit-blame-styles
              '((margin
                 (margin-format " %a - %s%f" " %C" " %H")
                 (margin-width . 42)
                 (margin-face . magit-blame-margin)
                 (margin-body-face magit-blame-dimmed)))))
    #+END_SRC

*** Keybindings.

    #+BEGIN_SRC emacs-lisp
      (use-package evil-magit
        :after (evil-collection magit))
    #+END_SRC

** =org-mode=.

*** Keybindings.

    #+BEGIN_SRC emacs-lisp
      (defun my/open-notes ()
        "Opens my notes."
        (interactive)
        (find-file (expand-file-name "~/Notes/notes.org")))

      (leader-def
        "a n" '(my/open-notes :which-key "notes"))

      (major-def
        :keymaps 'org-mode-map
        "'" '(org-edit-special :which-key "src editor")
        "e" '(org-export-dispatch :which-key "export")
        "a" '(org-agenda :which-key "agenda")

        "d" '(:ignore t :which-key "date")
        "d s" '(org-schedule :which-key "schedule")

        "s" '(:ignore t :which-key "subtree")
        "s r" '(org-refile :which-key "refile"))
    #+END_SRC

*** Show bullets instead of stars.

    #+BEGIN_SRC emacs-lisp
      (use-package org-bullets
        :after org
        :commands org-bullets-mode
        :init
        (add-hook 'org-mode-hook 'org-bullets-mode))
    #+END_SRC

*** Hide leading stars.

    #+BEGIN_SRC emacs-lisp
      (defun my/set-org-hide-face ()
        "Sets org-hide face based on current theme"
        (interactive)
        (face-spec-set 'org-hide
                       `((t :foreground
                            ,(symbol-value (intern (format "%s-base00" my/current-theme)))))))

      (setq org-hide-leading-stars t)
      (add-hook 'my/change-theme-hook 'my/set-org-hide-face)
    #+END_SRC

*** Change collapsed subtree symbol.

    #+BEGIN_SRC emacs-lisp
      (setq org-ellipsis " â†´")
    #+END_SRC

*** Make TAB act natively for code blocks.

    #+BEGIN_SRC emacs-lisp
      (setq org-src-tab-acts-natively t)
    #+END_SRC

*** Custom TODO keywords.

    #+BEGIN_SRC emacs-lisp
      (defun my/set-org-todo-keyword-faces ()
        "Sets org todo keyword faces based on current theme"
        (interactive)
        (setq org-todo-keyword-faces
              `(("TODO" . (:background
                           ,(symbol-value (intern (format "%s-base01" my/current-theme)))
                           :foreground
                           ,(symbol-value (intern (format "%s-base08" my/current-theme)))
                           :weight
                           bold))
                ("PROG" . (:background
                           ,(symbol-value (intern (format "%s-base01" my/current-theme)))
                           :foreground
                           ,(symbol-value (intern (format "%s-base0D" my/current-theme)))
                           :weight
                           bold))
                ("DONE" . (:background
                           ,(symbol-value (intern (format "%s-base01" my/current-theme)))
                           :foreground
                           ,(symbol-value (intern (format "%s-base0B" my/current-theme)))
                           :weight
                           bold)))))

      (setq org-todo-keywords '((sequence "TODO" "PROG" "|" "DONE")))
      (setq org-log-done t)
      (add-hook 'my/change-theme-hook 'my/set-org-todo-keyword-faces)
    #+END_SRC

*** Agenda files.

    #+BEGIN_SRC emacs-lisp
      (setq org-agenda-files (list "~/Notes/notes.org"))
    #+END_SRC

*** Better =org-refile=.

    #+BEGIN_SRC emacs-lisp
      (setq org-refile-targets '((org-agenda-files :maxlevel . 2) (my/configuration-path :maxlevel . 2)))
      (setq org-refile-use-outline-path 'file)
      (setq org-outline-path-complete-in-steps nil)
      (setq org-refile-allow-creating-parent-nodes 'confirm)
    #+END_SRC

*** Enable =evil-org=.

    #+BEGIN_SRC emacs-lisp
      (use-package evil-org
        :after (evil-collection org)
        :config
        (add-hook 'org-mode-hook 'evil-org-mode)
        (add-hook 'evil-org-mode-hook 'evil-org-set-key-theme)
        (require 'evil-org-agenda)
        (evil-org-agenda-set-keys))
    #+END_SRC

*** Presentations with =ox-reveal=.

    #+BEGIN_SRC emacs-lisp
      (use-package ox-reveal
        :config
        (setq org-reveal-root "https://cdnjs.cloudflare.com/ajax/libs/reveal.js/3.8.0"))
    #+END_SRC

*** Allow bind keywords for export.

    #+BEGIN_SRC emacs-lisp
      (setq org-export-allow-bind-keywords t)
    #+END_SRC

** =harvest=.

*** Initialize.

    #+BEGIN_SRC emacs-lisp
      (use-package reaper
        :commands reaper
        :init
        (setq reaper-hours-timer-mode nil)
        (setq reaper-api-key (getenv "HARVEST_API_KEY"))
        (setq reaper-account-id (getenv "HARVEST_ACCOUNT_ID")))
    #+END_SRC

*** Keybindings.

    #+BEGIN_SRC emacs-lisp
      (leader-def
        "a h" '(reaper :which-key "harvest"))

      (general-def
        :states '(normal visual)
        :keymaps 'reaper-mode-map
        "q" 'kill-buffer-and-window
        "g r" '(reaper-refresh :which-key "refresh"))

      (major-def
        :keymaps 'reaper-mode-map
        "d" '(reaper-goto-date :which-key "date")
        "s" '(reaper-start-timer :which-key "start timer")
        "S" '(reaper-stop-timer :which-key "stop timer")
        "n" '(reaper-start-new-timer :which-key "new timer")
        "e" '(reaper-edit-entry-time :which-key "edit time")
        "E" '(reaper-edit-entry :which-key "edit entry")
        "x" '(reaper-delete-entry :which-key "delete"))
    #+END_SRC

* Programming languages and modes.

** =html=.

   #+BEGIN_SRC emacs-lisp
     (use-package web-mode
       :mode
       ("\\.html?\\'" . web-mode)
       ("\\.php\\'" . web-mode))

     (use-package emmet-mode
       :commands emmet-mode
       :init
       (add-hook 'sgml-mode-hook 'emmet-mode)
       (add-hook 'css-mode-hook 'emmet-mode)
       :config
       (emmet-preview-mode 0)
       (general-def
         :definer 'minor-mode
         :states 'insert
         :keymaps 'emmet-mode
         "TAB" 'emmet-expand-line))
   #+END_SRC

** =elm=.

*** Helper functions.

    #+BEGIN_SRC emacs-lisp
      (defun my/elm-outline-mode ()
        "Enables outline mode for Elm files."
        (progn
          (outline-minor-mode)
          (setq outline-regexp "--+\ ")))

      (defun elm-mode-generate-tags ()
        "Generate a TAGS file for the current project."
        (interactive)
        (when (elm--has-dependency-file)
          (let* ((default-directory (elm--find-dependency-file-path))
                 (find-command "find . -type f -name \"*.elm\" -print")
                 (exclude-command (if elm-tags-exclude-elm-stuff
                                      (concat find-command " | egrep -v elm-stuff")
                                    find-command))
                 (etags-command (concat
                                 exclude-command
                                 " | egrep -v node_modules"
                                 " | etags --language=none --regex=@"
                                 (shell-quote-argument elm-tags-regexps)
                                 " -")))
            (call-process-shell-command (concat etags-command "&") nil 0))))

      (defun my/elm-import ()
        "Imports a module from prompted string."
        (interactive)
        (let ((statement (read-string "Import statement: " "import ")))
          (save-excursion
            (goto-char (point-min))
            (if (re-search-forward "^import " nil t)
                (beginning-of-line)
              (forward-line 1)
              (insert "\n"))
            (insert (concat statement "\n"))
            (save-buffer))))
    #+END_SRC

*** Initialization.

    #+BEGIN_SRC emacs-lisp
      (use-package elm-mode
        :commands elm-mode
        :init
        (setq elm-package-json "elm.json")
        (setq elm-tags-on-save t)
        (setq elm-format-on-save t)
        (setq elm-imenu-use-categories nil)
        :config
        (remove-hook 'elm-mode-hook 'elm-indent-mode)
        (add-hook 'elm-mode-hook 'flycheck-mode)
        (add-hook 'elm-mode-hook 'my/elm-outline-mode))

      (use-package flycheck-elm
        :after (flycheck elm-mode)
        :config
        (add-hook 'flycheck-mode-hook 'flycheck-elm-setup))
    #+END_SRC

*** Keybindings.

    #+BEGIN_SRC emacs-lisp
      (general-def
        :states '(normal visual)
        :keymaps 'elm-mode-map
        "TAB" 'org-cycle
        "<backtab>" 'org-global-cycle
        "M-<up>" 'outline-move-subtree-up
        "M-<down>" 'outline-move-subtree-down
        "g k" '(outline-previous-heading :which-key "previous heading")
        "g j" '(outline-next-heading :which-key "next heading"))

      (major-def
        :keymaps 'elm-mode-map
        "i" '(my/elm-import :which-key "import")
        "e" '(elm-expose-at-point :which-key "expose")
        "d" '(elm-documentation-lookup :which-key "documentation"))
    #+END_SRC

** =javascript=.

*** Initialization.

    #+BEGIN_SRC emacs-lisp
      (use-package js2-mode
        :mode ("\\.js\\'" . js2-mode)
        :config
        (setq js2-mode-show-parse-errors nil)
        (setq js2-mode-show-strict-warnings nil)
        (setq flycheck-javascript-eslint-executable "eslint_d")
        (add-hook 'js2-mode-hook 'flycheck-mode)
        (add-hook 'js2-mode-hook 'js2-imenu-extras-mode))
    #+END_SRC

*** Run =eslint fix= on file save.

    #+BEGIN_SRC emacs-lisp
      (use-package eslintd-fix
        :after js2-mode
        :commands eslintd-fix
        :init
        (add-hook 'js2-mode-hook
                  (lambda () (add-hook 'flycheck-before-syntax-check-hook #'eslintd-fix nil 'local))))
    #+END_SRC

*** =node=.

**** REPL.

     #+BEGIN_SRC emacs-lisp
       (use-package nodejs-repl
         :commands nodejs-repl)
     #+END_SRC

**** Modules path.

     #+BEGIN_SRC emacs-lisp
       (use-package add-node-modules-path
         :after (elm-mode js2-mode)
         :commands add-node-modules-path
         :init
         (add-hook 'js2-mode-hook #'add-node-modules-path)
         (add-hook 'elm-mode-hook #'add-node-modules-path))
     #+END_SRC

** =json=.

   #+BEGIN_SRC emacs-lisp
     (use-package json-mode
       :commands json-mode
       :config
       (major-def
         :keymaps 'json-mode-map
         "p" '(json-mode-show-path :which-key "path")))
   #+END_SRC

** =eshell=.

   #+BEGIN_SRC emacs-lisp
     (advice-add 'ansi-color-apply-on-region :before 'my/ansi-color-apply-on-region)

     (defun my/ansi-color-apply-on-region (begin end)
       "Fix progress bars for e.g. apt(8). Display progress in the mode line instead."
       (let ((end-marker (copy-marker end)) mb)
         (save-excursion
           (goto-char (copy-marker begin))
           (while (re-search-forward "\0337" end-marker t)
             (setq mb (match-beginning 0))
             (when (re-search-forward "\0338" end-marker t)
               (let ((progress (buffer-substring-no-properties
                                (+ mb 2) (- (point) 2))))
                 (delete-region mb (point))
                 (my/apt-progress-message progress)))))))

     (defun my/apt-progress-message (progress)
       (message
        (replace-regexp-in-string
         "%" "%%"
         (ansi-color-apply progress))))

     (add-hook 'eshell-mode-hook
               (lambda () (general-def
                            :keymaps 'eshell-mode-map
                            :states '(normal visual insert)
                            "C-j" 'eshell-next-input
                            "C-k" 'eshell-previous-input)))
   #+END_SRC

** =guix=.

   #+BEGIN_SRC emacs-lisp
     (use-package guix
       :commands guix
       :init
       (leader-def
         "a g" '(guix :which-key "guix")))
   #+END_SRC

** =nix=.

   #+BEGIN_SRC emacs-lisp
     (use-package nix-mode
       :mode "\\.nix\\'"
       :config
       (add-hook 'before-save-hook 'nix-mode-format))

     (use-package company-nixos-options
       :after (nix-mode nixos-options)
       :config
       (add-to-list 'company-backends 'company-nixos-options))
   #+END_SRC

** =yaml=.

   #+BEGIN_SRC emacs-lisp
     (use-package yaml-mode
       :mode "\\.yaml\\'")
   #+END_SRC

** =vagrant=.

   #+BEGIN_SRC emacs-lisp
     (use-package vagrant)
   #+END_SRC

* Communication and connection.

** =elfeed= RSS reader.

   #+BEGIN_SRC emacs-lisp
     (use-package elfeed
       :commands elfeed
       :init
       (leader-def
         "a f" 'elfeed)
       (major-def
         :keymaps 'elfeed-search-mode-map
         "u" '(elfeed-update :which-key "update")))

     (use-package elfeed-org
       :after (elfeed org)
       :init
       (setq rmh-elfeed-org-files (list "~/Notes/rss.org"))
       :config
       (elfeed-org))
   #+END_SRC

** IRC.

   #+BEGIN_SRC emacs-lisp
     (major-def
       :keymaps 'rcirc-mode-map
       "j" '(rcirc-cmd-join :which-key "join"))
   #+END_SRC

** VPN.

*** Helper functions.

    #+BEGIN_SRC emacs-lisp
      (defconst my/vpn-conf (expand-file-name "~/vpn/do.ovpn"))

      (defun my/connect-vpn ()
        "Connects to VPN"
        (interactive)
        (ovpn-mode-start-vpn-conf my/vpn-conf))

      (defun my/disconnect-vpn ()
        "Disconnects from VPN"
        (interactive)
        (ovpn-mode-stop-vpn-conf my/vpn-conf))
    #+END_SRC

*** Initialization.

    #+BEGIN_SRC emacs-lisp
      (use-package ovpn-mode
        :init
        (leader-def
          "a v" '(:ignore t :which-key "vpn")
          "a v c" '(my/connect-vpn :which-key "connect")
          "a v d" '(my/disconnect-vpn :which-key "disconnect")))
    #+END_SRC

* Help.

** =which-key= shows all available keybindings in a popup.

   #+BEGIN_SRC emacs-lisp
     (use-package which-key
       :config
       (which-key-mode 1))
   #+END_SRC

** =helpful= provides *Help* buffer on steroids.

   #+BEGIN_SRC emacs-lisp
     (use-package helpful
       :init
       (setq counsel-describe-function-function #'helpful-callable)
       (setq counsel-describe-variable-function #'helpful-variable)
       :config
       (general-def
         :states '(normal visual)
         :keymaps 'helpful-mode-map
         "q" 'kill-buffer-and-window))
   #+END_SRC

** Benchmark emacs initialization.

   #+BEGIN_SRC emacs-lisp
     (use-package benchmark-init
       :config
       (add-hook 'after-init-hook 'benchmark-init/deactivate))
   #+END_SRC

** Select help window when open.

   #+BEGIN_SRC emacs-lisp
     (setq help-window-select t)
   #+END_SRC

** Display =apropos= buffer in same window.

   #+BEGIN_SRC emacs-lisp
     (add-to-list 'display-buffer-alist
                  '("*Apropos*" display-buffer-same-window))
   #+END_SRC

** =dash= documentation.

   #+BEGIN_SRC emacs-lisp
     (use-package counsel-dash
       :commands counsel-dash
       :init
       (setq counsel-dash-common-docsets '("JavaScript" "Lo-Dash"))
       (leader-def
         "a d" '(counsel-dash :which-key "dash")))
   #+END_SRC

** Thesaurus synonyms/antonyms.

   #+BEGIN_SRC emacs-lisp
     (use-package synosaurus
       :commands synosaurus-lookup)
   #+END_SRC

** Functions.

*** Exec npm command.

    #+BEGIN_SRC emacs-lisp
      (defun my/exec-npm (root-folder cmd-name)
        "Starts eshell buffer with given name, enters Conta's frontend folder and executes given npm command."
        (progn
          (if (get-buffer cmd-name)
              (switch-to-buffer cmd-name)
            (progn
              (eshell)
              (rename-buffer cmd-name)
              (insert "cd " root-folder " && npm run " cmd-name)
              (eshell-send-input)))))
    #+END_SRC

* Conta.

** Helper functions.

*** Frontend root folder.

    #+BEGIN_SRC emacs-lisp
      (defconst my/conta-frontend-root "~/contadev/front-end/")
    #+END_SRC

*** Serve frontend.

    #+BEGIN_SRC emacs-lisp
      (defun my/conta-frontend-serve ()
        "Starts Conta's frontend development server."
        (interactive)
        (my/exec-npm my/conta-frontend-root "dev:watch"))
    #+END_SRC

*** Run frontend JS tests.

    #+BEGIN_SRC emacs-lisp
      (defun my/conta-frontend-js-tests ()
        "Runs Conta's frontend JS unit tests command."
        (interactive)
        (my/exec-npm my/conta-frontend-root "tests:unit"))
    #+END_SRC

*** Run frontend JS tests coverage.

    #+BEGIN_SRC emacs-lisp
      (defun my/conta-frontend-js-tests-coverage ()
        "Runs Conta's frontend JS unit tests coverage command."
        (interactive)
        (my/exec-npm my/conta-frontend-root "tests:unit:enforce"))
    #+END_SRC

*** Clean elm stuff.

    #+BEGIN_SRC emacs-lisp
      (defun my/conta-frontend-elm-clean-stuff ()
        "Cleans Conta's frontend Elm stuff."
        (interactive)
        (my/exec-npm my/conta-frontend-root "elm:clean:stuff"))
    #+END_SRC

*** Update frontend.

    #+BEGIN_SRC emacs-lisp
      (defun my/conta-frontend-update ()
        "Updates Conta's frontend to the latest version."
        (interactive)
        (my/exec-npm my/conta-frontend-root "update"))
    #+END_SRC

** Keybindings.

   #+BEGIN_SRC emacs-lisp
     (leader-def
       "a c" '(:ignore t :which-key "conta")

       "a c u" '(my/conta-frontend-update :which-key "update")

       "a c s" '(:ignore t :which-key "serve")
       "a c s f" '(my/conta-frontend-serve :which-key "frontend")

       "a c e" '(:ignore t :which-key "elm")
       "a c e c" '(my/conta-frontend-elm-clean-stuff :which-key "clean stuff")

       "a c j" '(:ignore t :which-key "javascript")
       "a c j t" '(my/conta-frontend-js-tests :which-key "test")
       "a c j c" '(my/conta-frontend-js-tests-coverage :which-key "coverage"))
   #+END_SRC

* Moontells.

** Helper functions.

*** Root folder.

    #+BEGIN_SRC emacs-lisp
      (defconst my/moontells-root "~/moontells/")
    #+END_SRC

*** Serve mobile app.

    #+BEGIN_SRC emacs-lisp
      (defun my/moontells-mobile-serve ()
        "Starts Moontells's mobile app development server."
        (interactive)
        (my/exec-npm my/moontells-root "start:app"))
    #+END_SRC

** Keybindings.

   #+BEGIN_SRC emacs-lisp
     (leader-def
       "a m" '(:ignore t :which-key "moontells")

       "a m m" '(:ignore t :which-key "mobile")
       "a m m s" '(my/moontells-mobile-serve :which-key "serve"))
   #+END_SRC

* The End!
